// This solution will require an Event Grid to dynamically ingest the data from the blob container.
// Creating the Event Grid is the last step in this process.  The tables, mapping, and function have to be implemented first.

// 1. INTERNAL TABLE | BLOB CONTAINER SOLUTION
.create table WinSecEventsRaw (records:dynamic)

// 2. Set the ingest json mapping ..this puts each JSON object as it's own record
.create table WinSecEventsRaw ingestion json mapping 'WinSecEventsRawMapping' '[{"column":"records","Properties":{"path":"$.records"}}]'

// 3. Set the retention policy
.alter-merge table WinSecEventsRaw policy retention softdelete = 0d

// 4. Create the internal table
.create table WinSecEvents (
    TimeGenerated: datetime,
    PublisherId: string,
    TimeCreated: datetime,
    PublisherName: string,
    Channel: string,
    LoggingComputer: string,
    EventID: string,
    EventCategory: string,
    EventLevel: string,
    UserName: string,
    RawXml: string,
    EventDescription: string,
    RenderingInfo: string,
    EventRecordId: string,
    Keywords: string,
    Type: string
)

// 5. Create the Expand function that will process each record
.create-or-alter function WinSecEventsExpand() {
    WinSecEventsRaw
    | mv-expand event = records 
    | where isnotempty(todatetime(event.['time']))
    | project
        TimeGenerated = todatetime(event['time']),
        PublisherId = tostring(event.PublisherId),
        TimeCreated = todatetime(event.TimeCreated),
        PublisherName = tostring(event.PublisherName),
        Channel = tostring(event.Channel),
        LoggingComputer = tostring(event.LoggingComputer),
        EventID = tostring(event.EventNumber),
        EventCategory = tostring(event.EventCategory),
        EventLevel = tostring(event.EventLevel),
        UserName = tostring(event.UserName),
        RawXml = tostring(event.RawXml),
        EventDescription = tostring(event.EventDescription),
        RenderingInfo = tostring(event.RenderingInfo),
        EventRecordId = tostring(event.EventRecordId),
        Keywords = tostring(event.Keywords),
        Type = 'WinSecEvents'
}

// 6. Map the function WinSecEventsExpand() from the WinSecEventsRaw table and apply the result on to the WinSecEvents table!
.alter table WinSecEvents policy update @'[{"Source": "WinSecEventsRaw", "Query": "WinSecEventsExpand()", "IsEnabled": true, "IsTransactional": true}]'

// END OF INTERNAL TABLE SOLUTION
